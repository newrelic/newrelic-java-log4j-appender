plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.14.1'
}

jar {
    enabled = false // Disable the standard JAR task
}

shadowJar {
    archiveClassifier.set('') // Remove the 'all' classifier
    mergeServiceFiles()
    manifest {
        attributes(
            'Implementation-Title': 'Custom Log4j2 Appender',
            'Implementation-Vendor': 'New Relic Labs',
            'Implementation-Vendor-Id': 'com.newrelic.labs',
            'Implementation-Version': '1.0.3'
        )
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar // Only include Shadow JAR
            artifact javadocJar
            artifact sourcesJar

            groupId = 'com.newrelic.labs'
            artifactId = 'custom-log4j2-appender'
            version = '1.0.3'

            pom {
                name = 'Custom Log4j2 Appender'
                description = 'A custom Log4j2 appender for New Relic.'
                url = 'https://github.com/newrelic/newrelic-java-log4j-appender'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'newrelic'
                        name = 'New Relic'
                        email = 'gsidhwani@newrelic.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/newrelic/newrelic-java-log4j-appender.git'
                    developerConnection = 'scm:git:ssh://github.com/newrelic/newrelic-java-log4j-appender.git'
                    url = 'https://github.com/newrelic/newrelic-java-log4j-appender'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'sonatype'
            url = uri('https://oss.sonatype.org/service/local/staging/deploy/maven2/')
            credentials {
                username = project.findProperty("ossrhUsername") ?: ""
                password = project.findProperty("ossrhPassword") ?: ""
            }
        }
    }
}